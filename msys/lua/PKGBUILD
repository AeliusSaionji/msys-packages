# Hacked together for msys2 by Aelius
# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>
# Contributor: Felix Huettner <huettner94@gmx.de>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=lua
pkgbase=${_realname}
pkgname=${_realname}
pkgver=5.4.6
pkgrel=2
pkgdesc="A powerful light-weight programming language designed for extending applications. (msys)"
arch=('i686' 'x86_64')
url="https://www.lua.org/"
license=('spdx:MIT')
makedepends=('gcc' 'ccache')
source=("${url}/ftp/lua-${pkgver}.tar.gz"
        'lua.pc'
        '0001-When-running-lua-on-mintty-or-other.patch'
        '0002-Add-Wl-out-implib-liblua.dll.a-to-AR.patch'
        '0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch'
        'link-implib.patch'
        'posix.patch'
        'LICENSE')
sha256sums=('7d5ea1b9cb6aa0b59ca3dde1c6adcb57ef83a1ba8e5432c0ecd06bf439b3ad88'
            'ca9252633e782b8f85d6a94ea4f6babd4fe30bd759085b373160b1878e36ff78'
            '37246417ce7b1d062cb52012c087793c961527e5b6da5318cf573244a477f528'
            '5f0c29865f9645a61ca62b1860f26ffac8b3458bd5e7b7a9c92daae0927a1914'
            '2294a245794fac0ec6a6bae990ca2cb7c77b8426026192b5446480abd2a2fd21'
            '0e9f1fab745c8d6cd1fe37a32c8bd6cca665692aac5c130237e648abef86f632'
            '5cac90a675687977d56f9a7ae4b44f782d5630b4dee1d6b328ce406d4f800dff'
            '142fb08b41a807b192b4b2c166696a1830a1c97967e5099ad0e579bf500e1da4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}/lua.pc" .
  rm -f src/iscygpty.{c,h} || true
  patch -p1 -i "${srcdir}/0001-When-running-lua-on-mintty-or-other.patch"
  patch -p1 -i "${srcdir}/0002-Add-Wl-out-implib-liblua.dll.a-to-AR.patch"
  patch -F3 -p1 -i "${srcdir}/0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch"
  patch -F3 -p1 -i "${srcdir}/link-implib.patch"
  patch -F3 -p1 -i "${srcdir}/posix.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  sed -e "s|%VER%|${pkgver%.*}|g;s|%REL%|${pkgver}|g" \
    -i lua.pc

  make \
    AR="ar rcu" \
    RANLIB="ranlib" \
    STRIP="strip" \
    CC="${CC}" \
    mingw
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make \
    TO_BIN="lua.exe luac.exe lua54.dll" \
    TO_LIB="liblua.a liblua.dll.a" \
    INSTALL_TOP="${pkgdir}/usr" \
    INSTALL_MAN="${pkgdir}/usr"/share/man/man1 \
    install

  install -Dm644 lua.pc "${pkgdir}/usr"/lib/pkgconfig/lua.pc
  install -Dm644 lua.pc "${pkgdir}/usr"/lib/pkgconfig/lua5.4.pc

  # Install the documentation
  install -d ${pkgdir}/usr/share/doc/lua
  install -m644 doc/*.{gif,png,css,html} ${pkgdir}/usr/share/doc/lua
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${_realname}/LICENSE
}
