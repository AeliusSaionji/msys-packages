# Maintainer: Jeremy Gust <jeremy AT plasticsoup DOT net>
# Contributor: Jameson Pugh <imntreal@gmail.com>

_pkgname=moar
pkgbase="mingw-w64-${_pkgname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_pkgname}")
pkgver=1.15.3
pkgrel=1
pkgdesc="Moar is a pager. It's designed to just do the right thing without any configuration."
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/walles/moar'
license=(BSD)
makedepends=("$MINGW_PACKAGE_PREFIX-go")
# optdepends=("highlight: source code syntax highlighter")
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/walles/moar/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('9cbfd11cc128052c66dde381d77b2c4bfb1260feb1e0a0d23119844b274fe659')

build() {

 export CGO_CPPFLAGS="${CPPFLAGS}"
 export CGO_CFLAGS="${CFLAGS}"
 export CGO_CXXFLAGS="${CXXFLAGS}"
 export CGO_LDFLAGS="${LDFLAGS}"
 export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd "${srcdir}/${_pkgname}-${pkgver}"

   go build -ldflags="-s -w -X main.versionString=${pkgver}" -o "${_pkgname}"
}

package() {

 cd "${srcdir}/${_pkgname}-${pkgver}"

  install -Dm755 moar "${pkgdir}${MINGW_PREFIX}/bin/moar"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_pkgname}/LICENSE"
  install -Dm644 moar.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/moar.1"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/${_pkgname}/README.md"
}
