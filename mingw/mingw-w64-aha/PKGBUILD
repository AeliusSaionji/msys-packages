_pkgname=aha
pkgbase="mingw-w64-${_pkgname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_pkgname}")
pkgver=0.5.1
pkgrel=1
pkgdesc='Ansi HTML Adapter: convert ANSI escape sequences to HTML'
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_pkgname}-git")
url='https://github.com/theZiz/aha'
license=('LGPLv2+' 'MPL1.1')
source=("https://github.com/theZiz/aha/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('6aea13487f6b5c3e453a447a67345f8095282f5acd97344466816b05ebd0b3b1')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE.LGPLv2+ "${pkgdir}${MINGW_PREFIX}/share/licenses/${_pkgname}/LICENSE.LGPLv2+"
  install -Dm644 LICENSE.MPL1.1 "${pkgdir}${MINGW_PREFIX}/share/licenses/${_pkgname}/LICENSE.MPL1.1"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/${_pkgname}/README.md"
}
