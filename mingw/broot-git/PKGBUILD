_pkgname=broot
pkgname=("${MINGW_PACKAGE_PREFIX}-${_pkgname}-git")
pkgver=1.21.3.1411.g79ab7ba
pkgrel=1
pkgdesc='Fuzzy Search + tree + cd; a better way to navigate directories'
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://dystroy.org/broot/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${_pkgname}::git+https://github.com/Canop/${_pkgname}.git")
sha256sums=('SKIP')
# options=('')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_pkgname}")

pkgver() {
  cd "${_pkgname}"
  echo "$(grep '^version =' Cargo.toml|head -n1|cut -d\" -f2|cut -d\- -f1).$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "${_pkgname}"
  cargo build --release
}

package() {
  cd "${_pkgname}"
  install -Dt "${pkgdir}${MINGW_PREFIX}/bin" "target/release/$_pkgname"
  sed -i "s/#version/$pkgver/" man/page
  # Theoretically we could get the date from the CHANGELOG.md but it seems that the
  # CHANGELOG.md entry for the current version isn't actually in the current release.
  # changelog_date=$(sed -n "s/.*v$pkgver - \(.*\)$/\1/p" CHANGELOG.md)
  sed -i "s/#date//" man/page
  gzip --best man/page
  install -Dm644 man/page.gz "${pkgdir}${MINGW_PREFIX}/share/man/man1/$_pkgname.1.gz"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/$_pkgname/LICENSE"
}

