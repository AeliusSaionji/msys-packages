_pkgname=broot
pkgbase="mingw-w64-${_pkgname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_pkgname}")
pkgver=1.31.0
pkgrel=1
pkgdesc='Fuzzy Search + tree + cd; a better way to navigate directories'
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://dystroy.org/broot/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/Canop/broot/archive/v$pkgver/$_pkgname-v$pkgver.tar.gz")
sha256sums=('8aee20cc8c339262dbfd1b064c261cf9628ec671124f94412c23343bbdcb691b')
# options=('')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_pkgname}-git")

build() {
  cd $_pkgname-$pkgver
  cargo build --release --locked #--frozen
}

package() {
  cd $_pkgname-$pkgver
  install -Dt "${pkgdir}${MINGW_PREFIX}/bin" "target/release/$_pkgname"
  sed -i "s/#version/$pkgver/" man/page
  # Theoretically we could get the date from the CHANGELOG.md but it seems that the
  # CHANGELOG.md entry for the current version isn't actually in the current release.
  # changelog_date=$(sed -n "s/.*v$pkgver - \(.*\)$/\1/p" CHANGELOG.md)
  sed -i "s/#date//" man/page
  install -Dm644 man/page "${pkgdir}${MINGW_PREFIX}/share/man/man1/$_pkgname.1"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/$_pkgname/LICENSE"
  install -Dm644 resources/icons/vscode/vscode.ttf "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF/vscode.ttf"
}
