_realname=senpai
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=v0.3.0.r4.g09129df
pkgrel=1
pkgdesc='terminal irc client written in go'
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://git.sr.ht/~delthas/senpai/'
license=('ISC')
makedepends=("${MINGW_PACKAGE_PREFIX}-scdoc" "${MINGW_PACKAGE_PREFIX}-go")
source=('git+https://git.sr.ht/~delthas/senpai')
sha256sums=('SKIP')
options=('!strip') #won't run otherwise

pkgver() {
  cd "${srcdir}/senpai"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/senpai"
  #various fixes for mystery problems -aelius
  go env -w CGO_ENABLED=0 GOFLAGS="-buildvcs=false"
  make all
}

package() {
  cd "${srcdir}/senpai"
  make PREFIX=/ DESTDIR="${pkgdir}${MINGW_PREFIX}" install
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/${pkgname}/README.md"
}
